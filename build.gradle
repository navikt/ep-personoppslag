
buildscript {
    ext {
        springBootVersion = '5.3.18'
        springWebVersion = '2.6.4'
        jacksonModuleKotlinVersion = '2.13.3'
        prometheusVersion = '1.8.4'
        eessiMetricsVersion = '0.4.29'
        eessiEpLoggingVersion = '1.0.24'
        mockkVersion = '1.12.4'
        oidcTokenSupportVersion = '1.3.9'
    }

    repositories {
        mavenCentral()
    }
}

plugins {
    id("java-library")
    id("maven-publish")
    id("org.jetbrains.kotlin.jvm") version "1.7.0"
    id("net.researchgate.release") version "3.0.0"
    id("org.jetbrains.kotlin.plugin.spring") version "1.7.0"
    id("com.adarshr.test-logger") version "3.2.0"
    id("com.github.ben-manes.versions") version "0.42.0"
    id("se.patrikerdes.use-latest-versions") version "0.2.18"
    id("org.owasp.dependencycheck") version "7.1.1"
    id("com.vanniktech.dependency.graph.generator") version "0.8.0"
    id "org.sonarqube" version "3.4.0.2513"
    id 'jacoco'
}

group = "no.nav.eessi.pensjon"

java {
    withJavadocJar()
    withSourcesJar()
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "17"
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "17"
    }
}

// https://docs.gradle.org/current/userguide/jacoco_plugin.html
jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

tasks.sonarqube.dependsOn tasks["jacocoTestReport"]

dependencies {
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.7.0"))
    implementation("org.jetbrains.kotlin:kotlin-gradle-plugin")
    implementation("org.jetbrains.kotlin:kotlin-reflect")


    // Logging
    implementation('net.logstash.logback:logstash-logback-encoder:7.0.1') {
        exclude module: "commons-logging"
    }

    implementation("io.micrometer:micrometer-registry-prometheus:$prometheusVersion")

    implementation("no.nav.eessi.pensjon:ep-metrics:$eessiMetricsVersion")
    implementation("no.nav.eessi.pensjon:ep-logging:$eessiEpLoggingVersion")

    // Spring
    implementation("org.springframework.boot:spring-boot-starter-web:$springWebVersion") {
        exclude module : "tomcat-embed-core"
    }
    implementation("org.springframework:spring-web:$springBootVersion")

    //Jackson json
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonModuleKotlinVersion")

    implementation("javax.servlet:javax.servlet-api:4.0.1")

    testImplementation(platform("org.junit:junit-bom:5.8.2"))
    testImplementation("org.junit.jupiter:junit-jupiter")
    testImplementation("org.springframework:spring-test:$springBootVersion")
    testImplementation("io.mockk:mockk:$mockkVersion")

    //Auth
    implementation("no.nav.security:token-client-spring:${oidcTokenSupportVersion}")
    implementation("no.nav.security:token-validation-spring:${oidcTokenSupportVersion}")
    implementation("no.nav.security:token-validation-jaxrs:${oidcTokenSupportVersion}")
    implementation("no.nav.security:token-validation-test-support:${oidcTokenSupportVersion}")


    configurations { all*.exclude group: 'commons-logging', module: 'commons-logging' }
}

test {
    useJUnitPlatform()
    failFast = true
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }
}

// https://github.com/researchgate/gradle-release
release {
    newVersionCommitMessage = "[Release Plugin] - next version commit: "
    tagTemplate = "release-\${version}"

    git {
        requireBranch.set('master')
    }
}

// https://help.github.com/en/actions/language-and-framework-guides/publishing-java-packages-with-gradle#publishing-packages-to-github-packages
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/navikt/ep-personoppslag"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

repositories {
    mavenCentral()

    ["ep-metrics", "ep-logging"].each { repo ->
        String token = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.key")
        if (!token) throw new NullPointerException("Missing token, you have to set GITHUB_TOKEN or gpr.key, see README")
        maven {
            url = uri("https://maven.pkg.github.com/navikt/$repo")
            credentials {
                username = "token"
                password = token
            }
        }
    }
}


/* https://github.com/ben-manes/gradle-versions-plugin */
dependencyUpdates {
    rejectVersionIf {
        ['alpha', 'beta', 'b', 'rc', 'cr', 'm', 'preview', 'pr']
                .any { qualifier -> it.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/}
    }
    revision = 'release'
}
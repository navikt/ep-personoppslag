buildscript {
    ext {
        springFrameworkVersion = '5.3.22'
        springBootVersion = '2.7.3'
        jacksonModuleKotlinVersion = '2.13.3'
        eessiMetricsVersion = '0.5.11'
        eessiEpLoggingVersion = '1.0.39'
        mockkVersion = '1.12.5'
        tokenSupportVersion = '2.1.4'
    }
}

plugins {
    id("com.vanniktech.dependency.graph.generator") version "0.8.0"
}

apply from: "https://raw.githubusercontent.com/navikt/ep-script-plugins/release-0.2.0/ep-module.gradle"
apply from: "https://raw.githubusercontent.com/navikt/ep-script-plugins/release-0.2.0/ep-library.gradle"

group = "no.nav.eessi.pensjon"

dependencies {
    implementation("org.jetbrains.kotlin:kotlin-reflect")

    // Logging
    implementation('net.logstash.logback:logstash-logback-encoder:7.2') {
        exclude module: "commons-logging"
    }

    implementation("no.nav.eessi.pensjon:ep-metrics:$eessiMetricsVersion")
    implementation("no.nav.eessi.pensjon:ep-logging:$eessiEpLoggingVersion")

    // Spring-boot BOM-fil spesifiserer versjoner for Spring Boot-avhengigheter
    implementation(platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}"))
    implementation("org.springframework.retry:spring-retry:1.3.3")
    implementation("org.springframework.boot:spring-boot-starter-web") {
        exclude module : "tomcat-embed-core"
    }

    // Spring Framework BOM-fil overstyrer så vi får siste patch-versjon av Spring Framework
    implementation(platform("org.springframework:spring-framework-bom:$springFrameworkVersion"))
    implementation("org.springframework:spring-web")

    //Jackson json
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonModuleKotlinVersion")

    implementation("javax.servlet:javax.servlet-api:4.0.1")

    testImplementation(platform("org.junit:junit-bom:5.9.0"))
    testImplementation("org.junit.jupiter:junit-jupiter")
    testImplementation("org.springframework:spring-test")
    testImplementation("io.mockk:mockk:$mockkVersion")

    //Auth
    implementation("no.nav.security:token-client-spring:${tokenSupportVersion}")
    implementation("no.nav.security:token-validation-spring:${tokenSupportVersion}")
    implementation("no.nav.security:token-validation-jaxrs:${tokenSupportVersion}")
    implementation("no.nav.security:token-validation-test-support:2.0.5")

    configurations { all*.exclude group: 'commons-logging', module: 'commons-logging' }
}

test {
    failFast = true
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }
}

// https://github.com/researchgate/gradle-release
release {
    git {
        requireBranch.set('master')
    }
}

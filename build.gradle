
buildscript {
    ext {
        springBootVersion = '5.3.20'
        springWebVersion = '2.7.0'
        kotlinVersion = '1.6.21'
        jacksonModuleKotlinVersion = '2.13.3'
        prometheusVersion = '1.8.1'
        eessiMetricsVersion = '0.4.+'
        eessiEpLoggingVersion = '1.0.+'
        eessiEpSecVersion = '0.0.+'
        junitVersion = '5.8.2'
        mockkVersion = '1.12.4'
    }

    repositories {
        mavenCentral()
    }
}

plugins {
    id("java-library")
    id("maven-publish")
    id("org.jetbrains.kotlin.jvm") version "1.6.21"
    id("net.researchgate.release") version "2.8.1"
    id("org.jetbrains.kotlin.plugin.spring") version "1.6.21"
    id("com.adarshr.test-logger") version "3.2.0"
    id("com.github.ben-manes.versions") version "0.42.0"
    id("se.patrikerdes.use-latest-versions") version "0.2.18"
    id("org.owasp.dependencycheck") version "7.1.0.1"
    id("com.vanniktech.dependency.graph.generator") version "0.7.0"
    id "org.sonarqube" version "3.3"
    id 'jacoco'
}

group = "no.nav.eessi.pensjon"

java {
    withJavadocJar()
    withSourcesJar()
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "17"
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "17"
    }
}

// https://docs.gradle.org/current/userguide/jacoco_plugin.html
jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

tasks.sonarqube.dependsOn tasks["jacocoTestReport"]

dependencies {
    implementation(platform("org.jetbrains.kotlin:kotlin-bom"))
    implementation("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
    implementation("org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion")


    // Logging
    implementation('net.logstash.logback:logstash-logback-encoder:7.2') {
        exclude module: "commons-logging"
    }

    implementation("io.micrometer:micrometer-registry-prometheus:$prometheusVersion")

    //eessi-pensjon
    implementation("no.nav.eessi.pensjon:ep-security-sts:$eessiEpSecVersion")
    implementation("no.nav.eessi.pensjon:ep-metrics:$eessiMetricsVersion")
    implementation("no.nav.eessi.pensjon:ep-logging:$eessiEpLoggingVersion")

    // Spring
    implementation("org.springframework.boot:spring-boot-starter-web:$springWebVersion") {
        exclude module : "tomcat-embed-core"
    }
    implementation("org.springframework:spring-web:$springBootVersion")
    implementation('org.springframework.retry:spring-retry:1.3.3')

    //Jackson json
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonModuleKotlinVersion")

    implementation("javax.servlet:javax.servlet-api:4.0.1")

    testImplementation("org.junit.jupiter:junit-jupiter:$junitVersion")
    testImplementation("org.springframework:spring-test:$springBootVersion")
    testImplementation("io.mockk:mockk:$mockkVersion")


    configurations { all*.exclude group: 'commons-logging', module: 'commons-logging' }
}

test {
    useJUnitPlatform()
    failFast = true
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }
}

// https://github.com/researchgate/gradle-release
release {
    newVersionCommitMessage = "[Release Plugin] - next version commit: "
    tagTemplate = "release-\${version}"
}

// https://help.github.com/en/actions/language-and-framework-guides/publishing-java-packages-with-gradle#publishing-packages-to-github-packages
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/navikt/ep-personoppslag"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

repositories {
    mavenCentral()

    ["ep-metrics", "ep-logging", "ep-security-sts"].each { repo ->
        String token = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.key")
        if (!token) throw new NullPointerException("Missing token, you have to set GITHUB_TOKEN or gpr.key, see README")
        maven {
            url = uri("https://maven.pkg.github.com/navikt/$repo")
            credentials {
                username = "token"
                password = token
            }
        }
    }
}


/* https://github.com/ben-manes/gradle-versions-plugin */
dependencyUpdates {
    rejectVersionIf {
        ['alpha', 'beta', 'b', 'rc', 'cr', 'm', 'preview', 'pr']
                .any { qualifier -> it.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/}
    }
    revision = 'release'
}